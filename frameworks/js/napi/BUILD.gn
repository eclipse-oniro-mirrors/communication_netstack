# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")

SUBSYSTEM_DIR = "//foundation/communication"
NETSTACK_NAPI_ROOT = "$SUBSYSTEM_DIR/netstack/frameworks/js/napi/"

utils_source = [
  "$SUBSYSTEM_DIR/netstack/utils/base_context/src/netstack_base_context.cpp",
  "$SUBSYSTEM_DIR/netstack/utils/common_utils/src/netstack_common_utils.cpp",
  "$SUBSYSTEM_DIR/netstack/utils/event_manager/src/netstack_event_listener.cpp",
  "$SUBSYSTEM_DIR/netstack/utils/event_manager/src/netstack_event_manager.cpp",
  "$SUBSYSTEM_DIR/netstack/utils/module_template/src/netstack_module_template.cpp",
  "$SUBSYSTEM_DIR/netstack/utils/napi_utils/src/netstack_napi_utils.cpp",
]

utils_include = [
  "$SUBSYSTEM_DIR/netstack/utils/base_async_work/include",
  "$SUBSYSTEM_DIR/netstack/utils/base_context/include",
  "$SUBSYSTEM_DIR/netstack/utils/common_utils/include",
  "$SUBSYSTEM_DIR/netstack/utils/event_manager/include",
  "$SUBSYSTEM_DIR/netstack/utils/log/include",
  "$SUBSYSTEM_DIR/netstack/utils/module_template/include",
  "$SUBSYSTEM_DIR/netstack/utils/napi_utils/include",
]

common_include = [
  "//foundation/ace/ace_engine/frameworks/base/utils",
  "//foundation/ace/napi/interfaces/kits",
  "//foundation/ace/napi",
  "//third_party/node/src",
]

common_deps = [
  "//foundation/ace/napi/:ace_napi",
  "//utils/native/base:utils",
]

common_external_deps = [
  "hiviewdfx_hilog_native:libhilog",
  "ipc:ipc_core",
]

common_relative_install_dir = "module/net"
common_part_name = "netstack"
common_subsystem_name = "communication"

ohos_shared_library("http") {
  include_dirs = [
    "$NETSTACK_NAPI_ROOT/http/async_context/include",
    "$NETSTACK_NAPI_ROOT/http/async_work/include",
    "$NETSTACK_NAPI_ROOT/http/constant/include",
    "$NETSTACK_NAPI_ROOT/http/http_exec/include",
    "$NETSTACK_NAPI_ROOT/http/http_module/include",
    "$NETSTACK_NAPI_ROOT/http/options/include",
  ]
  include_dirs += utils_include
  include_dirs += common_include
  include_dirs += [ "//third_party/curl/include" ]

  sources = [
    "http/async_context/src/request_context.cpp",
    "http/async_work/src/http_async_work.cpp",
    "http/constant/src/constant.cpp",
    "http/http_exec/src/http_exec.cpp",
    "http/http_module/src/http_module.cpp",
    "http/options/src/http_request_options.cpp",
    "http/options/src/http_response.cpp",
  ]
  sources += utils_source

  deps = common_deps
  deps += [ "//third_party/curl:curl" ]

  external_deps = common_external_deps

  relative_install_dir = common_relative_install_dir
  part_name = common_part_name
  subsystem_name = common_subsystem_name
}

ohos_shared_library("socket") {
  include_dirs = [
    "$NETSTACK_NAPI_ROOT/socket/async_context/include",
    "$NETSTACK_NAPI_ROOT/socket/async_work/include",
    "$NETSTACK_NAPI_ROOT/socket/constant/include",
    "$NETSTACK_NAPI_ROOT/socket/socket_exec/include",
    "$NETSTACK_NAPI_ROOT/socket/socket_module/include",
    "$NETSTACK_NAPI_ROOT/socket/options/include",
    "$NETSTACK_NAPI_ROOT/socket/task_queue/include",
  ]
  include_dirs += utils_include
  include_dirs += common_include

  sources = [
    "socket/async_context/src/bind_context.cpp",
    "socket/async_context/src/common_context.cpp",
    "socket/async_context/src/connect_context.cpp",
    "socket/async_context/src/tcp_extra_context.cpp",
    "socket/async_context/src/tcp_send_context.cpp",
    "socket/async_context/src/udp_extra_context.cpp",
    "socket/async_context/src/udp_send_context.cpp",
    "socket/async_work/src/socket_async_work.cpp",
    "socket/options/src/extra_options_base.cpp",
    "socket/options/src/net_address.cpp",
    "socket/options/src/socket_remote_info.cpp",
    "socket/options/src/socket_state_base.cpp",
    "socket/options/src/tcp_connect_options.cpp",
    "socket/options/src/tcp_extra_options.cpp",
    "socket/options/src/tcp_send_options.cpp",
    "socket/options/src/udp_extra_options.cpp",
    "socket/options/src/udp_send_options.cpp",
    "socket/socket_exec/src/socket_exec.cpp",
    "socket/socket_module/src/socket_module.cpp",
    "socket/task_queue/src/task_queue.cpp",
  ]
  sources += utils_source

  deps = common_deps

  external_deps = common_external_deps

  relative_install_dir = common_relative_install_dir
  part_name = common_part_name
  subsystem_name = common_subsystem_name
}

if ("${product_name}" == "m40") {
  ohos_shared_library("websocket") {
    include_dirs = [
      "websocket/async_context/include",
      "websocket/async_work/include",
      "websocket/constant/include",
      "websocket/websocket_exec/include",
      "websocket/websocket_module/include",
    ]
    include_dirs += utils_include
    include_dirs += common_include
    include_dirs += [
      "//third_party/libwebsockets/include",
      "//third_party/openssl/include",
    ]

    sources = [
      "websocket/async_context/src/close_context.cpp",
      "websocket/async_context/src/connect_context.cpp",
      "websocket/async_context/src/send_context.cpp",
      "websocket/async_work/src/websocket_async_work.cpp",
      "websocket/constant/src/constant.cpp",
      "websocket/websocket_exec/src/websocket_exec.cpp",
      "websocket/websocket_module/src/websocket_module.cpp",
    ]
    sources += utils_source

    deps = common_deps
    deps += [
      "//third_party/libwebsockets:websockets",
      "//third_party/openssl:libcrypto_static",
      "//third_party/openssl:ssl_source",
      "//third_party/zlib:libz",
    ]

    external_deps = common_external_deps

    relative_install_dir = common_relative_install_dir
    part_name = common_part_name
    subsystem_name = common_subsystem_name
  }
} else {
  ohos_shared_library("websocket") {
    include_dirs = [ "websocket/include" ]
    include_dirs += utils_include
    include_dirs += common_include

    sources = [ "websocket/src/websocket_napi.cpp" ]

    deps = common_deps

    external_deps = common_external_deps

    relative_install_dir = common_relative_install_dir
    part_name = common_part_name
    subsystem_name = common_subsystem_name
  }
}

ohos_shared_library("fetch") {
  include_dirs = [
    "$NETSTACK_NAPI_ROOT/fetch/async_context/include",
    "$NETSTACK_NAPI_ROOT/fetch/async_work/include",
    "$NETSTACK_NAPI_ROOT/fetch/constant/include",
    "$NETSTACK_NAPI_ROOT/fetch/fetch_exec/include",
    "$NETSTACK_NAPI_ROOT/fetch/fetch_module/include",
    "$NETSTACK_NAPI_ROOT/fetch/options/include",
  ]
  include_dirs += utils_include
  include_dirs += common_include
  include_dirs += [ "//third_party/curl/include" ]

  sources = [
    "fetch/async_context/src/fetch_context.cpp",
    "fetch/async_work/src/fetch_async_work.cpp",
    "fetch/constant/src/constant.cpp",
    "fetch/fetch_exec/src/fetch_exec.cpp",
    "fetch/fetch_module/src/fetch_module.cpp",
    "fetch/options/src/fetch_request.cpp",
    "fetch/options/src/fetch_response.cpp",
  ]
  sources += utils_source

  deps = common_deps
  deps += [ "//third_party/curl:curl" ]

  external_deps = common_external_deps

  relative_install_dir = "module"
  part_name = common_part_name
  subsystem_name = common_subsystem_name
}
