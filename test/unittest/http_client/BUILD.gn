# Copyright (c) 2023-2024 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//build/ohos.gni")
import("//build/test.gni")
import("//foundation/communication/netstack/netstack_config.gni")

utils_include = [
  "$SUBSYSTEM_DIR/netstack/utils/common_utils/include",
  "$SUBSYSTEM_DIR/netstack/utils/log/include",
  "//foundation/communication/netstack/frameworks/native/http/http_client/include",
]

common_external_deps = [
  "c_utils:utils",
  "hilog:libhilog",
]

ohos_unittest("http_client_unittest") {
  sanitize = {
    cfi = true
    cfi_cross_dso = true
    debug = false
  }

  branch_protector_ret = "pac_ret"

  module_out_path = "netstack/netstack/http_client_unittest"

  include_dirs = [
    "$NETSTACK_INNERKITS_DIR/http_client/include",
    "$NETSTACK_INNERKITS_DIR/http_client/cache/base64/include",
    "$NETSTACK_INNERKITS_DIR/http_client/cache/cache_strategy/include",
    "$NETSTACK_INNERKITS_DIR/http_client/cache/lru_cache/include",
  ]
  include_dirs += utils_include

  sources = [
    "$NETSTACK_INNERKITS_DIR/http_client/cache/base64/src/http_client_base64_utils.cpp",
    "HttpClientErrorTest.cpp",
    "HttpClientRequestTest.cpp",
    "HttpClientResponseTest.cpp",
    "HttpClientTaskTest.cpp",
    "HttpClientTest.cpp",
    "HttpClientTlsConfigTest.cpp",
    "cache/Base64Test.cpp",
    "cache/CacheProxyTest.cpp",
    "cache/HttpCacheStrategyTest.cpp",
    "cache/HttpClientLruCacheTest.cpp",
  ]

  deps = [ "$NETSTACK_INNERKITS_DIR/http_client:http_client" ]

  defines = []
  external_deps = common_external_deps
  external_deps += [
    "cJSON:cjson",
    "curl:curl_shared",
    "googletest:gmock_main",
    "openssl:libssl_shared",
    "samgr:samgr_proxy",
  ]

  if (defined(global_parts_info) &&
      defined(global_parts_info.communication_netmanager_base) &&
      global_parts_info.communication_netmanager_base) {
    external_deps += [
      "netmanager_base:net_conn_manager_if",
      "netmanager_base:net_security_config_if",
    ]
    defines += [
      "HAS_NETMANAGER_BASE=1",
      "HTTP_HANDOVER_FEATURE",
    ]
    include_dirs += [ "$SUBSYSTEM_DIR/netstack/utils/http_over_curl/include" ]
  }

  part_name = "netstack"
  subsystem_name = "communication"
}

group("unittest") {
  testonly = true
  deps = [ ":http_client_unittest" ]
}
